apply plugin: 'war'
apply plugin: 'com.bmuschko.cargo'

buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'com.bmuschko:gradle-cargo-plugin:2.0.3'
    }
}

repositories {
    mavenCentral()
    maven {
        url "http://download.java.net/maven/2"
    }
    maven {
        url 'https://repository.jboss.org/nexus/content/groups/public/'
    }
}

dependencies {
    providedCompile 'javax:javaee-api:7.0'

    // Without the wildfly-controller-client, you'll get following error:
    //   ContainerException: Failed to create deployer with implementation class WildFly8xRemoteDeployer
    cargo 'org.wildfly:wildfly-controller-client:8.1.0.Final'

    // Without cargo-ant you'll get following error:
    //   Problem: failed to create task or type cargo
    cargo 'org.codehaus.cargo:cargo-ant:1.4.8'
    
    compile "net.sf.dozer:dozer:5.5.1"
}

cargo {
    containerId = 'wildfly8x'
    local {
        installer {
            installUrl = "http://download.jboss.org/wildfly/8.2.0.Final/wildfly-8.2.0.Final.zip"
            downloadDir = file("$buildDir/download")
            extractDir = file("$buildDir/extract")
        }
    }
}
